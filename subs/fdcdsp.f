C*********************************************************************
C subroutine Finite Difference Coefficients DiSPlay ******************
C            -      -          -            - --    ******************
C Steve Gibbons Wed Jul 19 18:15:28 BST 2000                         C
C____________________________________________________________________C
C                                                                    C
C This routine displays the actual coefficients from the SVFDCF      C
C array.                                                             C
C____________________________________________________________________C
C                                                                    C
C Input variables :-                                                 C
C ===============                                                    C
C  Integer                                                           C
C  -------                                                           C
C                                                                    C
C     LU        : Logical unit number for output file.               C
C     ILNR      : Left-most radial grid node.                        C
C     IRNR      : Right-most radial grid node.                       C
C     NBN       : Number of bounding nodes. See above.               C
C     NFDCM     : Leading dimension of SVFDC. At least (2*NBN+1)     C
C     NR        : Number of radial grid nodes in each function.      C
C     NDRVM     : Limit on NDRVS. Array bound for SVFDC.             C
C     IHD       : Highest derivative requested.                      C
C     NDCS      : Maximum distinct finite difference schemes         C
C                  stored by the array SVFDC.                        C
C                                                                    C
C     IS        : Number of finite difference scheme used.           C
C                                                                    C
C  Double Precision                                                  C
C  ----------------                                                  C
C                                                                    C
C     SVFDC     : Finite difference coefficient matrix.              C
C                  Dimension ( NFDCM, NR, NDRVM+1, NDCS ).           C
C                   Array is generated by the routine svfdcf         C
C                 See documentation for SVFDCF for details.          C
C                                                                    C
C____________________________________________________________________C
C
C*********************************************************************
      SUBROUTINE FDCDSP( LU, ILNR, IRNR, NBN, NFDCM, NR, NDRVM, IHD,
     1                   NDCS, IS, SVFDC )
      IMPLICIT NONE
C____________________________________________________________________C
C Variable declarations - Parameters ................................C
      INTEGER LU, ILNR, IRNR, NBN, NFDCM, NR, NDRVM, IHD, NDCS, IS
      DOUBLE PRECISION SVFDC( NFDCM, NR, NDRVM+1, NDCS )
C____________________________________________________________________C
C Variable declarations - Working variables .........................C
C
      INTEGER IR, ID, ILN, IRN, ID1, IK, INODE
C____________________________________________________________________C
C START OF PROGRAM **************************************************C
C____________________________________________________________________C
C
      IF ( IHD.GT.9 ) THEN
        PRINT *,' Subroutine FDCDSP.'
        PRINT *,' IHD = ', IHD
        PRINT *,' Program aborted.'
        STOP
      ENDIF
C     .
      IF ( IS.GT.NDCS ) THEN
        PRINT *,' Subroutine FDCDSP.'
        PRINT *,' IS = ', IS,' NDCS = ', NDCS
        PRINT *,' Program aborted.'
        STOP
      ENDIF
C     .
      DO IR = ILNR, IRNR
        ILN = MAX(  1, IR - NBN )
        IRN = MIN( NR, IR + NBN )
        DO ID = 1, IHD
          WRITE ( LU, 79 )
          WRITE ( LU, 80 ) ID, IR
          ID1 = ID + 1
          DO INODE = ILN, IRN
            IK = INODE - IR + NBN + 1
            WRITE ( LU, 81 ) SVFDC( IK, IR, ID1, IS ), INODE
          ENDDO
        ENDDO
      ENDDO
 79   FORMAT('---------------------------------')
 80   FORMAT('f^(',I2,') at grid node ',I4,' = Sum of ...')
 81   FORMAT('   ',F20.8,' * f(r) at grid node ',I4) 
C81   FORMAT('   ',1PD16.7,' * f(r) at grid node ',I4) 
C     .
      RETURN
      END
C*********************************************************************
