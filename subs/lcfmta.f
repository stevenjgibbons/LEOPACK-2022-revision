C*********************************************************************
C subroutine Landau Couette Flow Matrix Terms Add ********************
C            -      -       -    -      -     -   ********************
C Steve Gibbons Sat Apr 29 16:03:02 BST 2000                         C
C____________________________________________________________________C
C                                                                    C
C Takes a matrix AMAT, zeroes it and adds the terms for the linear   C
C stability problem                                                  C
C                                                                    C
C  A_t = ( lambda + 2 i kappa x - x^2 ) A + d^2A/dx^2                C
C                                                                    C
C Radial function 1 is a_r (real part) and radial function 2 is      C
C a_i (imag. part).                                                  C
C                                                                    C
C Calls AMLICA with AMLCFT as source subroutine.                     C
C____________________________________________________________________C
C                                                                    C
C Input variables :-                                                 C
C ===============                                                    C
C  Integer                                                           C
C  -------                                                           C
C                                                                    C
C     NR        : Number of radial grid nodes.                       C
C                                                                    C
C     NBN       : Number of nodes on each side of point for          C
C                  central differences.                              C
C                                                                    C
C     N1        : First dimension of matrix, A.                      C
C     N2        : Second dimension of matrix, A.                     C
C                                                                    C
C     KL        : Number of lower diagonals in matrix                C
C     KU        : Number of upper diagonals in matrix                C
C     KLE       : Number of additional lower diagonals in matrix     C
C                                                                    C
C  Double Precision                                                  C
C  ----------------                                                  C
C                                                                    C
C     A         : Matrix. Dim ( N1, N2 )                             C
C     DPARS     : Array dim (*).                                     C
C                 DPARS( 1 ) = LAMBDA                                C
C                 DPARS( 2 ) = KAPPA                                 C
C                                                                    C
C     XARR      : Dim ( NR ). i^{th} element gives x_i.              C
C     SVFDC     : Finite difference coefficient matrix.              C
C                  Dimension ( 2*NBN+1, NR, 3, 1).                   C
C                   Array is generated by the routine svfdcf         C
C                 See documentation for SVFDCF for details.          C
C                                                                    C
C____________________________________________________________________C
C
C*********************************************************************
      SUBROUTINE LCFMTA( NR, NBN, N1, N2, KL, KU, KLE, A, DPARS,
     1                   XARR, SVFDC )
      IMPLICIT NONE
C____________________________________________________________________C
C Variable declarations - Parameters ................................C
      INTEGER NR, NBN, N1, N2, KL, KU, KLE
      DOUBLE PRECISION A( N1, N2 ), DPARS( * ), XARR( * ),
     1                 SVFDC( 2*NBN+1, NR, 3, 1 )
C____________________________________________________________________C
C Variable declarations - Working variables .........................C
C
      INTEGER IHI, IHO, ILNR, IRNR, IPARS( 1 ), IFORMF,
     1        INARR( 3 ), NDCS, IMF, IHD, IOP, NCFM, NDRVS, IS
      DOUBLE PRECISION ZERO, FAC, CVEC( 3 )
      PARAMETER ( ZERO = 0.0d0, IFORMF = 3, FAC = 1.0d0, NDRVS = 2 )
      EXTERNAL AMLCFT
C____________________________________________________________________C
C START OF PROGRAM **************************************************C
C____________________________________________________________________C
C
      IS   = 1
      IMF  = 1
      NCFM = 2*NBN+1
      NDCS = 1
      ILNR = 2
      IRNR = NR - 1
C
      INARR( 1 ) = IFORMF
      INARR( 2 ) = NR
      INARR( 3 ) = 2
C
C Zero the matrix A
C
      IOP = 0
      CALL MATOP( A, ZERO, N1, N2, IOP )
C
C Add contributions from a_r to a_r
C
      IHD        = 2
      IPARS( 1 ) = 1
      IHI        = 1
      IHO        = 1
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_i to a_i
C
      IHD        = 2
      IPARS( 1 ) = 2
      IHI        = 2
      IHO        = 2
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_i to a_r
C
      IHD        = 0
      IPARS( 1 ) = 3
      IHI        = 2
      IHO        = 1
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_r to a_i
C
      IHD        = 0
      IPARS( 1 ) = 4
      IHI        = 1
      IHO        = 2
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C
      RETURN
      END
C*********************************************************************
