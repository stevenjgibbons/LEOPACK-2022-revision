C*********************************************************************
C subroutine Landau Couette Flow Time Step Matrix ********************
C            -      -       -    -    -    -      ********************
C Steve Gibbons Sun May  7 18:01:03 BST 2000                         C
C____________________________________________________________________C
C                                                                    C
C Takes a matrix AMAT, zeroes it and adds the terms for semi         C
C implicit time-step mechanism.                                      C
C                                                                    C
C AMAT * ( a_r , a_i ) gives real part                               C
C                                                                    C
C [ 1 - deltat (1-c) (lambda-x^2) - deltat (1-c) d^/dx^2 ] a_r       C
C + 2 deltat (1-c) kappa x a_i                                       C
C                                                                    C
C       and imaginary part                                           C
C                                                                    C
C [ 1 - deltat (1-c) (lambda-x^2) - deltat (1-c) d^2/dx^2 ] a_i      C
C - 2 deltat (1-c) kappa x a_r                                       C
C                                                                    C
C Radial function 1 is a_r (real part) and radial function 2 is      C
C a_i (imag. part).                                                  C
C                                                                    C
C Calls AMLICA with AMLCFT and AMDLT as source subroutines.          C
C Does LU decomposition of matrix at end.                            C
C____________________________________________________________________C
C                                                                    C
C Input variables :-                                                 C
C ===============                                                    C
C  Integer                                                           C
C  -------                                                           C
C                                                                    C
C     NR        : Number of radial grid nodes.                       C
C                                                                    C
C     NBN       : Number of nodes on each side of point for          C
C                  central differences.                              C
C                                                                    C
C     N1        : First dimension of matrix, A.                      C
C     N2        : Second dimension of matrix, A.                     C
C                                                                    C
C     KL        : Number of lower diagonals in matrix                C
C     KU        : Number of upper diagonals in matrix                C
C     KLE       : Number of additional lower diagonals in matrix     C
C                                                                    C
C     IPIV      : Dim (N2). Array for LAPACK LU decompostion.        C
C                   (in the routine DGBTRF).                         C
C                                                                    C
C  Double Precision                                                  C
C  ----------------                                                  C
C                                                                    C
C     A         : Matrix. Dim ( N1, N2 )                             C
C     DPARS     : Array dim (*).                                     C
C                 DPARS( 1 ) = LAMBDA                                C
C                 DPARS( 2 ) = KAPPA                                 C
C                                                                    C
C     DELTAT    : Time step.                                         C
C     CFAC      : Implicit/Explicit ratio.                           C
C                 CFAC must be greater than 0 and less than 1.       C
C                 If cfac was 1, this would be implicit.             C
C                 If cfac was 0, this would be explicit.             C
C                 cfac = 0.5 --> Crank-Nicolson.                     C
C                                                                    C
C     XARR      : Dim ( NR ). i^{th} element gives x_i.              C
C     SVFDC     : Finite difference coefficient matrix.              C
C                  Dimension ( 2*NBN+1, NR, 3, 1).                   C
C                   Array is generated by the routine svfdcf         C
C                 See documentation for SVFDCF for details.          C
C                                                                    C
C____________________________________________________________________C
C
C*********************************************************************
      SUBROUTINE LCFTSM( NR, NBN, N1, N2, KL, KU, KLE, A, DPARS,
     1                   DELTAT, CFAC, XARR, SVFDC, IPIV )
      IMPLICIT NONE
C____________________________________________________________________C
C Variable declarations - Parameters ................................C
      INTEGER NR, NBN, N1, N2, KL, KU, KLE, IPIV( * )
      DOUBLE PRECISION A( N1, N2 ), DPARS( * ), XARR( * ),
     1                 SVFDC( 2*NBN+1, NR, 3, 1 ), DELTAT, CFAC
C____________________________________________________________________C
C Variable declarations - Working variables .........................C
C
      INTEGER IHI, IHO, ILNR, IRNR, IPARS( 1 ), IFORMF, INFO, 
     1        INARR( 3 ), NDCS, IMF, IHD, IOP, NCFM, NDRVS, IS,
     2        MHP( 2 ), MHBCS( 1 )
      DOUBLE PRECISION ZERO, FAC, CVEC( 3 ), DIAGEL
      PARAMETER ( ZERO = 0.0d0, IFORMF = 3, NDRVS = 2 )
      EXTERNAL AMLCFT, AMDLT
C____________________________________________________________________C
C START OF PROGRAM **************************************************C
C____________________________________________________________________C
C
      MHP( 1 )   = 1
      MHP( 2 )   = 1
      MHBCS( 1 ) = 3
C
      IF ( CFAC.LE.0.0d0 .OR. CFAC.GE.1.0d0 ) THEN
        PRINT *,' Subroutine LCFTSM.'
        PRINT *,' CFAC = ', CFAC
        PRINT *,' Program aborted.'
        STOP
      ENDIF
C
      IS   = 1
      IMF  = 1
      NCFM = 2*NBN+1
      NDCS = 1
C
      INARR( 1 ) = IFORMF
      INARR( 2 ) = NR
      INARR( 3 ) = 2
C
C Zero the matrix A
C
      IOP = 0
      CALL MATOP( A, ZERO, N1, N2, IOP )
C
      ILNR       = 2
      IRNR       = NR - 1
      IHD        = 0
      FAC        = 1.0d0
      IPARS( 1 ) = 1
      DO IHI = 1, 2
        CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHI, INARR,
     1               IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2               NDRVS, NDRVS, IPARS, AMDLT, A, FAC, XARR,
     3               CVEC, DPARS, SVFDC )
      ENDDO
C
      FAC = (CFAC-1.0d0)*DELTAT
C
C Add contributions from a_r to a_r
C
      IHD        = 2
      IPARS( 1 ) = 1
      IHI        = 1
      IHO        = 1
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_i to a_i
C
      IHD        = 2
      IPARS( 1 ) = 2
      IHI        = 2
      IHO        = 2
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_i to a_r
C
      IHD        = 0
      IPARS( 1 ) = 3
      IHI        = 2
      IHO        = 1
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Add contributions from a_r to a_i
C
      IHD        = 0
      IPARS( 1 ) = 4
      IHI        = 1
      IHO        = 2
C     .
      CALL AMLICA( N1, N2, KL, KU, KLE, IMF, IHI, IHO, INARR,
     1             IHD, NBN, ILNR, IRNR, NCFM, NR, NDCS, IS,
     2             NDRVS, NDRVS, IPARS, AMLCFT, A, FAC, XARR,
     3             CVEC, DPARS, SVFDC )
C
C Check on matrix singularities
C
      DIAGEL = 1.0d0
      CALL AMSDEA( A, N1, N2, KL, KU, KLE, IMF, INARR,
     1             MHP, MHBCS, 'Inner', DIAGEL, NDCS )
      CALL AMSDEA( A, N1, N2, KL, KU, KLE, IMF, INARR,
     1             MHP, MHBCS, 'Outer', DIAGEL, NDCS )
C
C Now we should be able to do an LU decomposition.
C
      CALL DGBTRF( N2, N2, KL, KL, A, N1, IPIV, INFO )
C     .
C     . Check for an error from LU decomp.
C     .
      IF ( INFO.NE.0 ) THEN
        PRINT *,' Subroutine LCFTSM.'
        PRINT *,' The LAPACK subroutine DGBTRF has'
        PRINT *,' returned ',INFO,' as a value of '
        PRINT *,' INFO in LU decomposition of matrix.'
        PRINT *,' Program aborted.'
        STOP
      ENDIF
C
      RETURN
      END
C*********************************************************************
